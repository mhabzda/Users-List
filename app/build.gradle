plugins {
    alias libs.plugins.android.application
    alias libs.plugins.junit5.android
    alias libs.plugins.kotlin.android
    alias libs.plugins.kotlin.kapt

    id "org.jetbrains.kotlin.android.extensions" version "1.6.10"
}

apply from: "../gradle/githubToken.gradle"

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.users"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "GITHUB_TOKEN", getGithubToken("$rootDir/github_token.txt"))

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation libs.androidx.appCompat
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.coreKtx
    implementation libs.androidx.recyclerview
    implementation libs.androidx.swiperefreshlayout

    implementation libs.dagger
    implementation libs.dagger.android
    kapt libs.dagger.compiler
    kapt libs.dagger.processor
    implementation libs.dagger.support

    implementation libs.rxAndroid
    implementation libs.rxJava
    implementation libs.rxKotlin

    implementation libs.moshi
    kapt libs.moshi.codegen
    implementation libs.okHttp
    implementation libs.retrofit
    implementation libs.retrofit.moshi
    implementation libs.retrofit.rxJava

    kapt libs.room.compiler
    implementation libs.room.runtime
    implementation libs.room.rxJava

    implementation libs.glide

    testImplementation libs.junit5.api
    testRuntimeOnly libs.junit5.engine
    testImplementation libs.junit5.params
    testImplementation libs.mockito.kotlin
}
