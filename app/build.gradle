plugins {
    alias libs.plugins.android.application
    alias libs.plugins.junit5.android
    alias libs.plugins.kotlin.android
    alias libs.plugins.kotlin.kapt
}

apply from: "$rootDir/gradle/githubToken.gradle"
apply from: "$rootDir/gradle/config.gradle"

kotlin {
    jvmToolchain config.jvmToolchain
}

android {
    namespace 'com.users'

    compileSdk config.compileSdkVersion

    defaultConfig {
        applicationId "com.users"
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "GITHUB_TOKEN", getGithubToken("$rootDir"))

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        dataBinding true
        buildConfig true
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation libs.androidx.appCompat
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.coreKtx
    implementation libs.androidx.recyclerview
    kapt libs.androidx.room.compiler
    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.rxJava
    implementation libs.androidx.swiperefreshlayout

    implementation libs.dagger
    implementation libs.dagger.android
    kapt libs.dagger.compiler
    kapt libs.dagger.processor
    implementation libs.dagger.support

    implementation libs.rxAndroid
    implementation libs.rxJava
    implementation libs.rxKotlin

    implementation libs.moshi
    kapt libs.moshi.codegen
    implementation libs.okHttp
    implementation libs.retrofit
    implementation libs.retrofit.moshi
    implementation libs.retrofit.rxJava

    implementation libs.glide

    testImplementation libs.junit5.api
    testRuntimeOnly libs.junit5.engine
    testImplementation libs.junit5.params
    testImplementation libs.mockito.kotlin
}
